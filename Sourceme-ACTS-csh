# -*-Mode: sh;-*-
# For csh or tcsh
# $Header: /Volumes/cvsrep/developer/OpenADFortTk/Attic/Sourceme-ACTS-csh,v 1.8 2003/12/02 13:20:14 eraxxon Exp $

#############################################################################
# These variables should be set by the user
#############################################################################

setenv WHIRL2XAIF "${HOME}/Develop/whirl2xaif"

set PLATFORM=`cd ${WHIRL2XAIF}/src/config; ./hpcplatform`
switch ($PLATFORM)
case i686-Cygwin:
  set base="/cygdrive/c/Tmp/ACTS"
  breaksw
case i686-Linux:
case ia64-Linux:
  set base="/local/eraxxon"
  breaksw
case mips-IRIX64:
case sparc-SunOS:
  set base="/net/basuri4/eraxxon"
  breaksw
default:
  # alpha-OSF1 -> /import/basuri4/eraxxon/HPCToolkit/obj/alpha
  # alpha-OSF1.net -> /net/basuri4/eraxxon/HPCToolkit/obj/alpha
  echo "PLATFORM '${PLATFORM}' is unknown..."
  breaksw
endsw

# Base directories of various components
setenv OPEN64_BASE "${base}/Open64"
setenv OPENANALYSIS_BASE "${HOME}/Develop/OpenAnalysis"
setenv XERCESC_BASE "${HOME}/Develop/xercesc"
setenv XAIFBOOSTER_BASE "${base}/ACTS"
setenv BOOST_BASE "${base}/ACTS/boost-1.30.2"
setenv ANGEL_BASE "${base}/ACTS/angel"

set XAIFSCHEMA="${base}/ACTS/xaif-1.0"

unset base

#############################################################################
# System dependent
#############################################################################

set PLATFORM=`cd ${WHIRL2XAIF}/src/config; ./hpcplatform`

switch ($PLATFORM)
  case i686-Linux:
  case i686-Cygwin:
    set o64targ="targ_ia32_ia64_linux"
    breaksw
  case ia64-Linux:
    set o64targ="targ_ia64_ia64_linux"
    breaksw
  case mips-IRIX64:
    set o64targ="targ_mips_irix"
    breaksw
  case sparc-SunOS:
    set o64targ="targ_sparc_solaris"
    breaksw
  default:
    echo "$PLATFORM '$$PLATFORM' is unknown..."
    breaksw
endsw

if ( "${PLATFORM}" == "i686-Cygwin" ) then 
  # convert to Windows path for XercesC
  set XAIFSCHEMA=`cygpath -w ${XAIFSCHEMA}`
endif

# Installations of the respective components
setenv OPEN64ROOT "${OPEN64_BASE}/osprey1.0/${o64targ}"
setenv OPENANALYSISROOT "${OPENANALYSIS_BASE}/${PLATFORM}"
setenv XERCESCROOT "${XERCESC_BASE}/${PLATFORM}"
setenv XAIFBOOSTERROOT "${XAIFBOOSTER_BASE}"
setenv BOOSTROOT "${BOOST_BASE}"
setenv ANGELROOT "${ANGEL_BASE}"

unset o64targ

#############################################################################
# Open64
#############################################################################

alias mfef90  ${OPEN64ROOT}/crayf90/sgi/mfef90
alias whirl2f ${OPEN64ROOT}/whirl2f/whirl2f
alias whirl2f90 ${OPEN64ROOT}/whirl2f/whirl2f90
alias ir_b2a  ${OPEN64ROOT}/ir_tools/ir_b2a
alias ir_size ${OPEN64ROOT}/ir_tools/ir_size

#############################################################################
# whirl2xaif
#############################################################################

source ${WHIRL2XAIF}/Sourceme-csh

setenv WHIRL2XAIF_OPEN64BASE "${OPEN64_BASE}/osprey1.0"
setenv WHIRL2XAIF_OPEN64  "${OPEN64ROOT}"
setenv WHIRL2XAIF_OA      "${OPENANALYSISROOT}"
setenv WHIRL2XAIF_XERCESC "${XERCESCROOT}"

#############################################################################
# Paths
#############################################################################

if ( "${PLATFORM}" == "i686-Cygwin" ) then 

  setenv PATH "${XERCESCROOT}/bin:${XERCESCROOT}/lib:${OPEN64ROOT}/be:${OPEN64ROOT}/whirl2f:${PATH}"

else 

  set lpath="${XERCESCROOT}/lib:${OPEN64ROOT}/whirl2f"
  if ( ! $?LD_LIBRARY_PATH ) then
    setenv LD_LIBRARY_PATH "${lpath}"
  else
    setenv LD_LIBRARY_PATH "${lpath}:${LD_LIBRARY_PATH}"
  endif
  unset lpath

endif


#############################################################################
# xaifbooster
#############################################################################

# Used by Angel
setenv XAIF_DIR "${XAIFBOOSTERROOT}"
setenv BOOST_DIR "${BOOSTROOT}"

set xbase="${XAIFBOOSTERROOT}/xaifBooster"
set ii_xaif="${XAIFSCHEMA}/schema/examples/inlinable_intrinsics.xaif"

alias xboostread "${xbase}/system/test/t -c ${ii_xaif}"
alias xboost_l "${xbase}/algorithms/Linearization/test/t -c ${ii_xaif}"
alias xboost_bb "${xbase}/algorithms/BasicBlockPreaccumulation/test/t -c ${ii_xaif}"

unset xbase
unset ii_xaif

#############################################################################

unset XAIFSCHEMA
unset PLATFORM
