# -*-Mode: sh;-*-
# For csh or tcsh
# $Header: /Volumes/cvsrep/developer/OpenADFortTk/Attic/Sourceme-ACTS-csh,v 1.6 2003/11/13 16:10:53 eraxxon Exp $

#############################################################################
# These variables should be set by the user
#############################################################################

setenv WHIRL2XAIF "${HOME}/Develop/whirl2xaif"

set PLATFORM=`cd ${WHIRL2XAIF}/src/config; ./hpcplatform`
switch ($PLATFORM)
case i686-Cygwin:
  set base="/cygdrive/c/Tmp/ACTS"
  breaksw
case i686-Linux:
case ia64-Linux:
  set base="/local/eraxxon"
  breaksw
case mips-IRIX64:
case sparc-SunOS:
  set base="/net/basuri4/eraxxon"
  breaksw
default:
  # alpha-OSF1 -> /import/basuri4/eraxxon/HPCToolkit/obj/alpha
  # alpha-OSF1.net -> /net/basuri4/eraxxon/HPCToolkit/obj/alpha
  echo "PLATFORM '${PLATFORM}' is unknown..."
  breaksw
endsw


setenv OPEN64ROOT "${base}/Open64"

setenv OPENANALYSISROOT "${HOME}/Develop/OpenAnalysis"
setenv XERCESCROOT "${HOME}/Develop/xercesc"

# This points to the *parent* directory of xaifBooster
setenv XAIFBOOSTERROOT "${base}/ACTS"
setenv BOOSTROOT "${base}/ACTS/boost-1.30.2"
setenv ANGELROOT "${base}/ACTS/angel"

set XAIFSCHEMA="${base}/ACTS/xaif-1.0"

unset base

#############################################################################
# System dependent
#############################################################################

set PLATFORM=`cd ${WHIRL2XAIF}/src/config; ./hpcplatform`

switch ($PLATFORM)
  case i686-Linux:
  case i686-Cygwin:
    set o64targ="targ_ia32_ia64_linux"
    breaksw
  case ia64-Linux:
    set o64targ="targ_ia64_ia64_linux"
    breaksw
  case mips-IRIX64:
    set o64targ="targ_mips_irix"
    breaksw
  case sparc-SunOS:
    set o64targ="targ_sparc_solaris"
    breaksw
  default:
    echo "$PLATFORM '$$PLATFORM' is unknown..."
    breaksw
endsw

setenv OPEN64TARG "${OPEN64ROOT}/osprey1.0/${o64targ}"

# reset XERCESCROOT
setenv XERCESCROOT "${XERCESCROOT}/${PLATFORM}"

unset o64targ

#############################################################################
# Open64
#############################################################################

alias mfef90  ${OPEN64TARG}/crayf90/sgi/mfef90
alias whirl2f ${OPEN64TARG}/whirl2f/whirl2f
alias whirl2f90 ${OPEN64TARG}/whirl2f/whirl2f90
alias ir_b2a  ${OPEN64TARG}/ir_tools/ir_b2a
alias ir_size ${OPEN64TARG}/ir_tools/ir_size

#############################################################################
# whirl2xaif
#############################################################################

source ${WHIRL2XAIF}/Sourceme-csh

setenv WHIRL2XAIF_OPEN64  "${OPEN64ROOT}"
setenv WHIRL2XAIF_OA      "${OPENANALYSISROOT}"
setenv WHIRL2XAIF_XERCESC "${XERCESCROOT}"

#############################################################################
# Paths
#############################################################################

if ( "${PLATFORM}" == "i686-Cygwin" ) then 

  setenv PATH "${XERCESCROOT}/bin:${XERCESCROOT}/lib:${OPEN64TARG}/be:${OPEN64TARG}/whirl2f:${PATH}"

else 

  if ( ! $?LD_LIBRARY_PATH ) then
    setenv LD_LIBRARY_PATH "${XERCESCROOT}/lib"
  else
    setenv LD_LIBRARY_PATH "${XERCESCROOT}/lib:${LD_LIBRARY_PATH}"
  endif

endif


#############################################################################
# xaifbooster
#############################################################################

# Used by Angel
setenv XAIF_DIR "${XAIFBOOSTERROOT}"
setenv BOOST_DIR "${BOOSTROOT}"

set xbase="${XAIFBOOSTERROOT}/xaifBooster"
set ii_xaif="${XAIFSCHEMA}/schema/examples/inlinable_intrinsics.xaif"

alias xboostread "${xbase}/system/test/t -c ${ii_xaif}"
alias xboost_l "${xbase}/algorithms/Linearization/test/t -c ${ii_xaif}"
alias xboost_bb "${xbase}/algorithms/BasicBlockPreaccumulation/test/t -c ${ii_xaif}"

unset xbase
unset ii_xaif

#############################################################################

unset XAIFSCHEMA
unset PLATFORM
